// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.29;

import {Test} from "forge-std/Test.sol";

import {UNIMinter} from "../src/UNIMinter.sol";
import {IUNIMinter} from "../src/interfaces/IUNIMinter.sol";
import {MockUNIToken} from "./mocks/MockUNIToken.sol";
import {IUNI} from "../src/interfaces/IUNI.sol";

contract UNIMinterTest is Test {
  UNIMinter public uniMinter;
  MockUNIToken public UNI;

  address public owner = makeAddr("owner");
  address public alice = makeAddr("alice");
  address public bob = makeAddr("bob");
  address public charlie = makeAddr("charlie");
  address public dave = makeAddr("dave");
  address public unauthorizedUser = makeAddr("UnauthorizedUser");

  uint16 constant DEFAULT_REVOCATION_DELAY_DAYS = 365;
  uint16 constant MINT_CAP_PERCENT = 2;
  uint16 constant MAX_UNITS = 10_000;

  event Transfer(address indexed from, address indexed to, uint256 value);

  function setUp() public {
    uniMinter = new UNIMinter(owner);
    deployCodeTo("MockUNIToken", address(uniMinter.UNI()));
    UNI = MockUNIToken(address(uniMinter.UNI()));
    UNI.setMinter(address(uniMinter));
    assertEq(UNI.totalSupply(), UNI.initialTotalSupply());
    assertEq(UNI.minter(), address(uniMinter));
  }

  function test_Constructor() public view {
    assertEq(uniMinter.owner(), owner);
    assertEq(uniMinter.totalUnits(), 0);
    assertEq(address(uniMinter.UNI()), 0x1f9840a85d5aF5bf1D1762F925BDADdC4201F984);
  }

  function test_GrantSplit_Single() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    assertEq(uniMinter.totalUnits(), 5000);
    (
      address recipient,
      uint16 units,
      uint16 revocationDelayDays,
      uint48 pendingRevocationTime,
      bool adjustedForRevocation
    ) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 5000);
    assertEq(revocationDelayDays, DEFAULT_REVOCATION_DELAY_DAYS);
    assertEq(pendingRevocationTime, 0);
    assertEq(adjustedForRevocation, false);
  }

  function test_GrantSplit_Multiple() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 3000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2000, 180);
    uniMinter.grantSplit(charlie, 1500, 90);
    vm.stopPrank();

    assertEq(uniMinter.totalUnits(), 6500);

    (address recipient0, uint16 units0, uint16 delay0,, bool adjusted0) = uniMinter.splits(0);
    assertEq(recipient0, alice);
    assertEq(units0, 3000);
    assertEq(delay0, DEFAULT_REVOCATION_DELAY_DAYS);
    assertEq(adjusted0, false);

    (address recipient1, uint16 units1, uint16 delay1,, bool adjusted1) = uniMinter.splits(1);
    assertEq(recipient1, bob);
    assertEq(units1, 2000);
    assertEq(delay1, 180);
    assertEq(adjusted1, false);

    (address recipient2, uint16 units2, uint16 delay2,, bool adjusted2) = uniMinter.splits(2);
    assertEq(recipient2, charlie);
    assertEq(units2, 1500);
    assertEq(delay2, 90);
    assertEq(adjusted2, false);
  }

  function test_GrantSplit_MaxUnits() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, MAX_UNITS, DEFAULT_REVOCATION_DELAY_DAYS);
    assertEq(uniMinter.totalUnits(), MAX_UNITS);
  }

  function test_GrantSplit_RevertInsufficientUnits() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 6000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.expectRevert(IUNIMinter.InsufficientUnits.selector);
    uniMinter.grantSplit(bob, 5000, DEFAULT_REVOCATION_DELAY_DAYS);
    vm.stopPrank();
  }

  function test_GrantSplit_RevertUnauthorized() public {
    vm.expectRevert("UNAUTHORIZED");
    vm.prank(unauthorizedUser);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);
  }

  function test_GrantSplit_SameRecipientMultipleTimes() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 2000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(alice, 3000, 180);
    vm.stopPrank();

    assertEq(uniMinter.totalUnits(), 5000);

    (address recipient0, uint16 units0, uint16 delay0,, bool adjusted0) = uniMinter.splits(0);
    assertEq(recipient0, alice);
    assertEq(units0, 2000);
    assertEq(delay0, DEFAULT_REVOCATION_DELAY_DAYS);
    assertEq(adjusted0, false);

    (address recipient1, uint16 units1, uint16 delay1,, bool adjusted1) = uniMinter.splits(1);
    assertEq(recipient1, alice);
    assertEq(units1, 3000);
    assertEq(delay1, 180);
    assertEq(adjusted1, false);
  }

  function test_Mint_SingleRecipientFullSplit() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, MAX_UNITS, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(UNI.mintingAllowedAfter());

    uint256 expectedMintAmount = UNI.totalSupply() * MINT_CAP_PERCENT / 100;

    vm.expectEmit(true, true, false, true);
    emit Transfer(address(0), address(uniMinter), expectedMintAmount);

    uniMinter.mint();

    assertEq(UNI.balanceOf(address(uniMinter)), 0);
    assertEq(UNI.balanceOf(alice), expectedMintAmount);
    assertEq(UNI.totalSupply(), UNI.initialTotalSupply() + expectedMintAmount);
  }

  function test_Mint_MultipleRecipients() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 3000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(charlie, 2000, DEFAULT_REVOCATION_DELAY_DAYS);
    vm.stopPrank();

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedMintAmount = expectedMintCap * MAX_UNITS / MAX_UNITS;

    uniMinter.mint();

    assertEq(UNI.balanceOf(address(uniMinter)), 0);
    assertEq(UNI.balanceOf(alice), expectedMintAmount * 5000 / MAX_UNITS);
    assertEq(UNI.balanceOf(bob), expectedMintAmount * 3000 / MAX_UNITS);
    assertEq(UNI.balanceOf(charlie), expectedMintAmount * 2000 / MAX_UNITS);
    assertEq(UNI.totalSupply(), totalSupplyBefore + expectedMintAmount);
  }

  function test_Mint_PartialSplit() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedTotalMint = expectedMintCap * 5000 / MAX_UNITS;
    uint256 expectedAliceAmount = expectedTotalMint * 5000 / uniMinter.totalUnits();

    uniMinter.mint();

    assertEq(UNI.balanceOf(address(uniMinter)), 0);
    assertEq(UNI.balanceOf(alice), expectedAliceAmount);
    assertEq(UNI.totalSupply(), totalSupplyBefore + expectedTotalMint);
  }

  function test_Mint_PartialSplit_MultipleRecipients() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedTotalMint = expectedMintCap * 7000 / MAX_UNITS;
    uint256 expectedAliceAmount = expectedTotalMint * 5000 / uniMinter.totalUnits();
    uint256 expectedBobAmount = expectedTotalMint * 2000 / uniMinter.totalUnits();

    uniMinter.mint();

    assertEq(UNI.balanceOf(address(uniMinter)), 0);
    assertEq(UNI.balanceOf(alice), expectedAliceAmount);
    assertEq(UNI.balanceOf(bob), expectedBobAmount);
    assertEq(UNI.totalSupply(), totalSupplyBefore + expectedTotalMint);
  }

  function test_Mint_RevertNoUnits() public {
    vm.warp(UNI.mintingAllowedAfter());

    vm.expectRevert(IUNIMinter.NoUnits.selector);
    uniMinter.mint();
  }

  function test_Mint_CalledByAnyone() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(UNI.mintingAllowedAfter());

    vm.prank(unauthorizedUser);
    uniMinter.mint();

    assertGt(UNI.balanceOf(alice), 0);
  }

  function test_Mint_ConsecutiveMints() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, MAX_UNITS, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(UNI.mintingAllowedAfter());
    uniMinter.mint();
    uint256 firstMintBalance = UNI.balanceOf(alice);

    vm.warp(block.timestamp + 365 days);
    uniMinter.mint();
    uint256 secondMintBalance = UNI.balanceOf(alice) - firstMintBalance;

    assertGt(secondMintBalance, firstMintBalance);
  }

  function test_InitiateRevokeSplit_Single() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    uint256 currentTime = block.timestamp;
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    (,, uint16 revocationDelayDays, uint48 pendingRevocationTime, bool adjusted) =
      uniMinter.splits(0);
    assertEq(revocationDelayDays, DEFAULT_REVOCATION_DELAY_DAYS);
    assertEq(pendingRevocationTime, currentTime + uint256(DEFAULT_REVOCATION_DELAY_DAYS) * 1 days);
    assertEq(adjusted, false);
  }

  function test_InitiateRevokeSplit_RevertUnauthorized() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.expectRevert("UNAUTHORIZED");
    vm.prank(unauthorizedUser);
    uniMinter.initiateRevokeSplit(0);
  }

  function test_InitiateRevokeSplit_MultipleSplits() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 3000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2000, 180);
    uniMinter.grantSplit(charlie, 1000, 90);

    uint256 currentTime = block.timestamp;
    uniMinter.initiateRevokeSplit(1);
    vm.stopPrank();

    (,,, uint48 pendingTime0, bool adjusted0) = uniMinter.splits(0);
    (,, uint16 delayDays1, uint48 pendingTime1, bool adjusted1) = uniMinter.splits(1);
    (,,, uint48 pendingTime2, bool adjusted2) = uniMinter.splits(2);

    assertEq(pendingTime0, 0);
    assertEq(pendingTime1, currentTime + delayDays1 * 1 days);
    assertEq(pendingTime2, 0);
    assertEq(adjusted0, false);
    assertEq(adjusted1, false);
    assertEq(adjusted2, false);
  }

  function test_RevokeSplit_StandardRemoval() public {
    uint16 shortDelay = 90; // 90 days delay
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, shortDelay);

    // Initiate revocation early enough that it completes before next mint
    vm.warp(100); // Start early in the period
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Wait for half the delay period
    vm.warp(block.timestamp + shortDelay * 1 days / 2);

    uniMinter.revokeSplit(0);

    assertEq(uniMinter.totalUnits(), 0);
    vm.expectRevert();
    uniMinter.splits(0);
  }

  function test_RevokeSplit_MultipleSplits() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 3000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2000, 180);
    uniMinter.grantSplit(charlie, 1000, 90);

    uniMinter.initiateRevokeSplit(1);
    vm.stopPrank();

    vm.warp(block.timestamp + 180 days);

    uniMinter.revokeSplit(1);

    assertEq(uniMinter.totalUnits(), 4000);

    (address recipient0, uint16 units0,,, bool adjusted0) = uniMinter.splits(0);
    assertEq(recipient0, alice);
    assertEq(units0, 3000);
    assertEq(adjusted0, false);

    (address recipient1, uint16 units1,,, bool adjusted1) = uniMinter.splits(1);
    assertEq(recipient1, charlie);
    assertEq(units1, 1000);
    assertEq(adjusted1, false);
  }

  function test_RevokeSplit_RevertNotPendingRevocation() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.expectRevert(IUNIMinter.NotPendingRevocation.selector);
    uniMinter.revokeSplit(0);
  }

  function test_RevokeSplit_BeforeNextMint() public {
    uint16 shortDelay = 90; // Use shorter delay to ensure it completes before next mint
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, shortDelay);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Set time so revocation completes before next mint
    vm.warp(block.timestamp + shortDelay * 1 days / 2);

    // Call revokeSplit - should succeed and remove split entirely
    uniMinter.revokeSplit(0);

    assertEq(uniMinter.totalUnits(), 0);
    vm.expectRevert();
    uniMinter.splits(0);
  }

  function test_RevokeSplit_CalledByAnyone() public {
    uint16 shortDelay = 90;
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, shortDelay);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    vm.warp(block.timestamp + shortDelay * 1 days);

    vm.prank(unauthorizedUser);
    uniMinter.revokeSplit(0);

    assertEq(uniMinter.totalUnits(), 0);
  }

  function test_RevokeSplit_LastElement() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 3000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2000, 180);

    uniMinter.initiateRevokeSplit(1);
    vm.stopPrank();

    vm.warp(block.timestamp + 180 days);

    uniMinter.revokeSplit(1);

    assertEq(uniMinter.totalUnits(), 3000);

    (address recipient0, uint16 units0,,, bool adjusted0) = uniMinter.splits(0);
    assertEq(recipient0, alice);
    assertEq(units0, 3000);
    assertEq(adjusted0, false);

    vm.expectRevert();
    uniMinter.splits(1);
  }

  function test_MintAfterRevocation() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 5000, 180);
    uniMinter.grantSplit(bob, 3000, DEFAULT_REVOCATION_DELAY_DAYS);

    uniMinter.initiateRevokeSplit(0);
    vm.stopPrank();

    vm.warp(block.timestamp + 180 days);
    uniMinter.revokeSplit(0);

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedTotalMint = expectedMintCap * 3000 / MAX_UNITS;
    uint256 expectedBobAmount = expectedTotalMint * 3000 / uniMinter.totalUnits();

    uniMinter.mint();

    assertEq(UNI.balanceOf(alice), 0);
    assertEq(UNI.balanceOf(bob), expectedBobAmount);
  }

  function test_ComplexScenario() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 4000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 3000, 180);
    uniMinter.grantSplit(charlie, 2000, 90);
    vm.stopPrank();

    vm.warp(UNI.mintingAllowedAfter());
    uniMinter.mint();

    uint256 aliceFirstMint = UNI.balanceOf(alice);
    uint256 bobFirstMint = UNI.balanceOf(bob);
    uint256 charlieFirstMint = UNI.balanceOf(charlie);

    assertGt(aliceFirstMint, 0);
    assertGt(bobFirstMint, 0);
    assertGt(charlieFirstMint, 0);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(1);

    vm.warp(block.timestamp + 180 days);
    uniMinter.revokeSplit(1);

    vm.prank(owner);
    uniMinter.grantSplit(dave, 1000, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(block.timestamp + 365 days);
    uniMinter.mint();

    assertGt(UNI.balanceOf(alice), aliceFirstMint);
    assertEq(UNI.balanceOf(bob), bobFirstMint);
    assertGt(UNI.balanceOf(charlie), charlieFirstMint);
    assertGt(UNI.balanceOf(dave), 0);
  }

  function testFuzz_GrantSplit(address recipient, uint16 units, uint16 delayDays) public {
    vm.assume(units <= MAX_UNITS);
    vm.assume(delayDays > 0);

    vm.prank(owner);
    uniMinter.grantSplit(recipient, units, delayDays);

    assertEq(uniMinter.totalUnits(), units);
    (address storedRecipient, uint16 storedunits, uint16 storedDelay,, bool storedAdjusted) =
      uniMinter.splits(0);
    assertEq(storedRecipient, recipient);
    assertEq(storedunits, units);
    assertEq(storedDelay, delayDays);
    assertEq(storedAdjusted, false);
  }

  function testFuzz_MultipleGrantSplit(
    address[3] memory recipients,
    uint16[3] memory units,
    uint16[3] memory delays
  ) public {
    uint256 totalUnits = 0;
    for (uint256 i = 0; i < 3; i++) {
      totalUnits += units[i];
      vm.assume(delays[i] > 0);
    }
    vm.assume(totalUnits <= MAX_UNITS);

    vm.startPrank(owner);
    for (uint256 i = 0; i < 3; i++) {
      uniMinter.grantSplit(recipients[i], units[i], delays[i]);
    }
    vm.stopPrank();

    assertEq(uniMinter.totalUnits(), totalUnits);

    for (uint256 i = 0; i < 3; i++) {
      (address recipient, uint16 _units, uint16 delay,, bool adjusted) = uniMinter.splits(i);
      assertEq(recipient, recipients[i]);
      assertEq(_units, units[i]);
      assertEq(delay, delays[i]);
      assertEq(adjusted, false);
    }
  }

  function testFuzz_MintDistribution(uint16[4] memory splits) public {
    uint256 totalAllocated = 0;
    address[4] memory recipients = [alice, bob, charlie, dave];

    for (uint256 i = 0; i < 4; i++) {
      totalAllocated += splits[i];
    }
    vm.assume(totalAllocated > 0 && totalAllocated <= MAX_UNITS);

    vm.startPrank(owner);
    for (uint256 i = 0; i < 4; i++) {
      if (splits[i] > 0) {
        uniMinter.grantSplit(recipients[i], splits[i], DEFAULT_REVOCATION_DELAY_DAYS);
      }
    }
    vm.stopPrank();

    vm.warp(UNI.mintingAllowedAfter());

    uint256 mintCap = UNI.totalSupply() * MINT_CAP_PERCENT / 100;
    uint256 expectedMintAmount = mintCap * totalAllocated / MAX_UNITS;

    uniMinter.mint();

    for (uint256 i = 0; i < 4; i++) {
      if (splits[i] > 0) {
        uint256 expectedBalance = expectedMintAmount * splits[i] / uniMinter.totalUnits();
        assertEq(UNI.balanceOf(recipients[i]), expectedBalance);
      }
    }
  }

  function test_ZeroUnitsGrant() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 0, DEFAULT_REVOCATION_DELAY_DAYS);

    assertEq(uniMinter.totalUnits(), 0);
    (address recipient, uint16 units, uint16 delay,, bool adjusted) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 0);
    assertEq(delay, DEFAULT_REVOCATION_DELAY_DAYS);
    assertEq(adjusted, false);
  }

  function test_EdgeCase_RevokeFirstOfMany() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 2500, 90);
    uniMinter.grantSplit(bob, 2500, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(charlie, 2500, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(dave, 2500, DEFAULT_REVOCATION_DELAY_DAYS);

    uniMinter.initiateRevokeSplit(0);
    vm.stopPrank();

    vm.warp(block.timestamp + 90 days);
    uniMinter.revokeSplit(0);

    assertEq(uniMinter.totalUnits(), 7500);

    (address recipient0, uint16 units0,,,) = uniMinter.splits(0);
    assertEq(recipient0, dave);
    assertEq(units0, 2500);

    (address recipient1, uint16 units1,,,) = uniMinter.splits(1);
    assertEq(recipient1, bob);
    assertEq(units1, 2500);

    (address recipient2, uint16 units2,,,) = uniMinter.splits(2);
    assertEq(recipient2, charlie);
    assertEq(units2, 2500);
  }

  function test_EdgeCase_RevokeMiddleOfMany() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 2500, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2500, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(charlie, 2500, 180);
    uniMinter.grantSplit(dave, 2500, DEFAULT_REVOCATION_DELAY_DAYS);

    uniMinter.initiateRevokeSplit(2);
    vm.stopPrank();

    vm.warp(block.timestamp + 180 days);
    uniMinter.revokeSplit(2);

    assertEq(uniMinter.totalUnits(), 7500);

    (address recipient0,,,,) = uniMinter.splits(0);
    assertEq(recipient0, alice);

    (address recipient1,,,,) = uniMinter.splits(1);
    assertEq(recipient1, bob);

    (address recipient2,,,,) = uniMinter.splits(2);
    assertEq(recipient2, dave);
  }

  function test_MintWithZeroUnitsInList() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 0, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 5000, DEFAULT_REVOCATION_DELAY_DAYS);
    vm.stopPrank();

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedTotalMint = expectedMintCap * 5000 / MAX_UNITS;
    uint256 expectedBobAmount = expectedTotalMint * 5000 / uniMinter.totalUnits();

    uniMinter.mint();

    assertEq(UNI.balanceOf(alice), 0);
    assertEq(UNI.balanceOf(bob), expectedBobAmount);
  }

  function test_ReInitiateRevocation() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, 180);

    uint256 firstTime = block.timestamp;
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    (,, uint16 delayDays, uint48 pendingTime1,) = uniMinter.splits(0);
    assertEq(pendingTime1, firstTime + delayDays * 1 days);

    vm.warp(block.timestamp + 90 days);
    uint256 secondTime = block.timestamp;
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    (,,, uint48 pendingTime2,) = uniMinter.splits(0);
    assertEq(pendingTime2, secondTime + delayDays * 1 days);
  }

  function test_MintBeforeRevocationComplete() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 5000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 5000, DEFAULT_REVOCATION_DELAY_DAYS);

    uniMinter.initiateRevokeSplit(0);
    vm.stopPrank();

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedMintAmount = expectedMintCap;

    uniMinter.mint();

    assertEq(UNI.balanceOf(alice), expectedMintAmount * 5000 / MAX_UNITS);
    assertEq(UNI.balanceOf(bob), expectedMintAmount * 5000 / MAX_UNITS);
  }

  function test_RevokeSplit_PartialMint_HalfwayThroughPeriod() public {
    uint16 delayDays = 180;
    vm.prank(owner);
    uniMinter.grantSplit(alice, 6000, delayDays);

    // Initiate revocation that will complete halfway through next mint period
    // Next mint at mintingAllowedAfter, revocation at mintingAllowedAfter + 182.5 days
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + 365 days / 2; // Halfway through next period

    // Calculate when to initiate to achieve this timing
    uint256 initiateTime = revocationCompleteTime - delayDays * 1 days;
    vm.warp(initiateTime);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Call revokeSplit - should reduce split to 50% (halfway through period)
    uniMinter.revokeSplit(0);

    assertEq(uniMinter.totalUnits(), 3000); // 50% of 6000
    (address recipient, uint16 units,,,) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 3000);
  }

  function test_RevokeSplit_PartialMint_QuarterThroughPeriod() public {
    uint16 delayDays = 180;
    vm.prank(owner);
    uniMinter.grantSplit(alice, 8000, delayDays);

    // Revocation completes 1/4 way through next mint period
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + 365 days / 4;
    uint256 initiateTime = revocationCompleteTime - delayDays * 1 days;
    vm.warp(initiateTime);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    uniMinter.revokeSplit(0);

    assertEq(uniMinter.totalUnits(), 2000); // 25% of 8000
    (address recipient, uint16 units,,,) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 2000);
  }

  function test_RevokeSplit_PartialMint_ThenFullRevoke() public {
    uint16 delayDays = 180;
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, delayDays);

    // Setup partial revocation
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + 365 days / 2;
    uint256 initiateTime = revocationCompleteTime - delayDays * 1 days;
    vm.warp(initiateTime);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // First call reduces split
    uniMinter.revokeSplit(0);
    (,,,, bool adjusted) = uniMinter.splits(0);
    assertEq(uniMinter.totalUnits(), 2500);
    assertEq(adjusted, true);

    // After the mint, the split can be fully removed
    vm.warp(nextMintTime);
    uniMinter.mint();

    // Now revoke again - should fully remove
    uniMinter.revokeSplit(0);
    assertEq(uniMinter.totalUnits(), 0);
    vm.expectRevert();
    uniMinter.splits(0);
  }

  function test_RevokeSplit_RevertIfRevocationTooFarInFuture() public {
    uint16 delayDays = 180;
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, delayDays);

    // Setup revocation that completes after the period following next mint
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + 365 days + 1; // Just after the period
    uint256 initiateTime = revocationCompleteTime - delayDays * 1 days;
    vm.warp(initiateTime);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Should revert as revocation is not ready yet
    vm.expectRevert(IUNIMinter.InvalidRevocation.selector);
    uniMinter.revokeSplit(0);
  }

  // New tests for 365-day delay feature
  function test_GrantSplit_With365DayDelay() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 7500, 365);

    assertEq(uniMinter.totalUnits(), 7500);
    (address recipient, uint16 units, uint16 delayDays, uint48 pendingRevocationTime,) =
      uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 7500);
    assertEq(delayDays, 365);
    assertEq(pendingRevocationTime, 0);
  }

  function test_RevokeSplit_365DayDelay_FullRevocation() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, 365);

    // Initiate revocation
    uint256 initiateTime = block.timestamp;
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Fast forward to when revocation is ready (exactly at next mint time)
    vm.warp(initiateTime + 365 days);

    // Execute revocation - when revocation time equals mint time, split are reduced to 0
    uniMinter.revokeSplit(0);

    // Units are reduced to 0 but entry still exists
    assertEq(uniMinter.totalUnits(), 0);
    (address recipient, uint16 units,,,) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 0);
  }

  function test_RevokeSplit_365DayDelay_PartialRevocation() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 10_000, 365);

    // Calculate timing for partial revocation (75% through next mint period)
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + (365 days * 3 / 4);
    uint256 initiateTime = revocationCompleteTime - 365 days;

    vm.warp(initiateTime);
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Execute partial revocation
    uniMinter.revokeSplit(0);

    // Should have 75% of original split (7500)
    assertEq(uniMinter.totalUnits(), 7500);
    (address recipient, uint16 units,,, bool adjusted) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 7500);
    assertEq(adjusted, true); // Should be marked as adjusted
  }

  function test_MultipleSplit_DifferentDelays() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 2500, 365); // 1 year delay
    uniMinter.grantSplit(bob, 2500, 180); // 180 days delay
    uniMinter.grantSplit(charlie, 2500, 90); // 90 days delay
    uniMinter.grantSplit(dave, 2500, 30); // 30 days delay
    vm.stopPrank();

    assertEq(uniMinter.totalUnits(), 10_000);

    // Verify each split has correct delay
    (,, uint16 delay0,,) = uniMinter.splits(0);
    (,, uint16 delay1,,) = uniMinter.splits(1);
    (,, uint16 delay2,,) = uniMinter.splits(2);
    (,, uint16 delay3,,) = uniMinter.splits(3);

    assertEq(delay0, 365);
    assertEq(delay1, 180);
    assertEq(delay2, 90);
    assertEq(delay3, 30);
  }

  function test_InitiateAndRevoke_365DayDelay_BeforeDelay() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, 365);

    // Start well before the next mint to ensure proper timing
    vm.warp(100);

    uint256 initiateTime = block.timestamp;
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // Try to revoke before delay is complete (after 364 days)
    vm.warp(initiateTime + 364 days);

    // At this point, revocation is not ready yet (needs 365 days)
    // But we may be past the next mint time, which would make it process as partial
    (,,, uint48 pendingTime,) = uniMinter.splits(0);
    uint256 mintTime = UNI.mintingAllowedAfter();

    if (pendingTime > mintTime && pendingTime - mintTime < 365 days) {
      // Would process as partial revocation
      uniMinter.revokeSplit(0);
      (, uint16 remainingUnits,,, bool adjusted) = uniMinter.splits(0);
      assertEq(adjusted, true);
      // The units might round down to 0 if very close to revocation time
      assertLe(remainingUnits, 5000); // Should not have more than original
    } else {
      // Should revert as not ready
      vm.expectRevert(IUNIMinter.InvalidRevocation.selector);
      uniMinter.revokeSplit(0);
    }

    // Now warp to exactly 365 days from initiation
    vm.warp(initiateTime + 365 days);

    // Should succeed now - may need to call again if split weren't fully removed
    if (uniMinter.totalUnits() > 0) uniMinter.revokeSplit(0);
    assertEq(uniMinter.totalUnits(), 0);
  }

  function test_Mint_WithMultiple365DayDelaySplits() public {
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 3000, 365);
    uniMinter.grantSplit(bob, 3000, 365);
    uniMinter.grantSplit(charlie, 4000, 365);
    vm.stopPrank();

    vm.warp(UNI.mintingAllowedAfter());

    uint256 totalSupplyBefore = UNI.totalSupply();
    uint256 expectedMintCap = totalSupplyBefore * MINT_CAP_PERCENT / 100;
    uint256 expectedMintAmount = expectedMintCap;

    uniMinter.mint();

    assertEq(UNI.balanceOf(alice), expectedMintAmount * 3000 / MAX_UNITS);
    assertEq(UNI.balanceOf(bob), expectedMintAmount * 3000 / MAX_UNITS);
    assertEq(UNI.balanceOf(charlie), expectedMintAmount * 4000 / MAX_UNITS);
  }

  function testFuzz_ConfigurableRevocationDelay(uint16 delayDays, uint16 units) public {
    vm.assume(delayDays > 0 && delayDays <= 730); // Max 2 years
    vm.assume(units > 0 && units <= MAX_UNITS);

    vm.prank(owner);
    uniMinter.grantSplit(alice, units, delayDays);

    (address recipient, uint16 _units, uint16 storedDelay, uint48 pendingTime, bool adjusted) =
      uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(_units, units);
    assertEq(storedDelay, delayDays);
    assertEq(pendingTime, 0);
    assertEq(adjusted, false);

    // Test revocation timing
    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    (,,, uint48 newPendingTime,) = uniMinter.splits(0);
    assertEq(newPendingTime, block.timestamp + uint256(delayDays) * 1 days);
  }

  function test_SetMinter_Success() public {
    address newMinter = makeAddr("newMinter");

    vm.prank(owner);
    uniMinter.setMinter(newMinter);

    assertEq(UNI.minter(), newMinter);
  }

  function test_SetMinter_RevertUnauthorized() public {
    address newMinter = makeAddr("newMinter");

    vm.expectRevert("UNAUTHORIZED");
    vm.prank(unauthorizedUser);
    uniMinter.setMinter(newMinter);
  }

  function test_SetMinter_DisablesMinting() public {
    // First grant split and verify minting works
    vm.prank(owner);
    uniMinter.grantSplit(alice, MAX_UNITS, DEFAULT_REVOCATION_DELAY_DAYS);

    vm.warp(UNI.mintingAllowedAfter());
    uniMinter.mint();
    assertGt(UNI.balanceOf(alice), 0);

    // Transfer minter role to a new address
    address newMinter = makeAddr("newMinter");
    vm.prank(owner);
    uniMinter.setMinter(newMinter);

    // Verify minting no longer works from this contract
    vm.warp(block.timestamp + 365 days);
    vm.expectRevert(); // Should revert as this contract is no longer the minter
    uniMinter.mint();
  }

  function test_SetMinter_TransferToZeroAddress() public {
    // Setting minter to zero address effectively burns the minting capability
    vm.prank(owner);
    uniMinter.setMinter(address(0));

    assertEq(UNI.minter(), address(0));
  }

  function test_SetMinter_TransferBackToOriginal() public {
    address newMinter = makeAddr("newMinter");

    // Transfer to new minter
    vm.prank(owner);
    uniMinter.setMinter(newMinter);
    assertEq(UNI.minter(), newMinter);

    // Mock the new minter transferring back
    vm.mockCall(
      address(UNI),
      abi.encodeWithSelector(IUNI.setMinter.selector, address(uniMinter)),
      abi.encode()
    );

    // New minter transfers back to original
    vm.prank(newMinter);
    UNI.setMinter(address(uniMinter));

    // Clear the mock to check actual state
    vm.clearMockedCalls();

    // In a real scenario, this would require the new minter to actually call setMinter
    // For testing purposes, we verify the concept works
  }

  function test_SetMinter_CalledMultipleTimes() public {
    address minter1 = makeAddr("minter1");
    address minter2 = makeAddr("minter2");

    vm.startPrank(owner);

    uniMinter.setMinter(minter1);
    assertEq(UNI.minter(), minter1);

    // After first transfer, subsequent calls from uniMinter should fail
    // since it's no longer the minter
    vm.expectRevert();
    uniMinter.setMinter(minter2);

    vm.stopPrank();
  }

  function test_SetMinter_DoesNotAffectSplits() public {
    // Grant split
    vm.startPrank(owner);
    uniMinter.grantSplit(alice, 3000, DEFAULT_REVOCATION_DELAY_DAYS);
    uniMinter.grantSplit(bob, 2000, DEFAULT_REVOCATION_DELAY_DAYS);
    vm.stopPrank();

    uint256 totalUnitsBefore = uniMinter.totalUnits();

    // Transfer minter role
    address newMinter = makeAddr("newMinter");
    vm.prank(owner);
    uniMinter.setMinter(newMinter);

    // Verify split are unchanged
    assertEq(uniMinter.totalUnits(), totalUnitsBefore);

    (address recipient0, uint16 units0,,,) = uniMinter.splits(0);
    assertEq(recipient0, alice);
    assertEq(units0, 3000);

    (address recipient1, uint16 units1,,,) = uniMinter.splits(1);
    assertEq(recipient1, bob);
    assertEq(units1, 2000);
  }

  function testFuzz_SetMinter(address newMinter) public {
    vm.prank(owner);
    uniMinter.setMinter(newMinter);

    assertEq(UNI.minter(), newMinter);
  }

  function test_MultipleMints_OverMultipleYears_Full2PercentInflation() public {
    // Grant full split to ensure full 2% inflation rate is allocated
    vm.prank(owner);
    uniMinter.grantSplit(alice, MAX_UNITS, DEFAULT_REVOCATION_DELAY_DAYS);

    // Year 1 mint
    vm.warp(UNI.mintingAllowedAfter());
    uint256 supplyBeforeYear1 = UNI.totalSupply();
    uint256 expectedYear1Mint = supplyBeforeYear1 * MINT_CAP_PERCENT / 100;

    uniMinter.mint();
    uint256 year1Balance = UNI.balanceOf(alice);
    assertEq(year1Balance, expectedYear1Mint);
    assertEq(UNI.totalSupply(), supplyBeforeYear1 + expectedYear1Mint);

    // Warp to Year 2
    vm.warp(block.timestamp + 365 days);
    uint256 supplyBeforeYear2 = UNI.totalSupply();
    uint256 expectedYear2Mint = supplyBeforeYear2 * MINT_CAP_PERCENT / 100;

    uniMinter.mint();
    uint256 year2Balance = UNI.balanceOf(alice) - year1Balance;
    assertEq(year2Balance, expectedYear2Mint);
    assertEq(UNI.totalSupply(), supplyBeforeYear2 + expectedYear2Mint);

    // Warp halfway through Year 3 (to show mid-year timing doesn't affect minting)
    vm.warp(block.timestamp + 182 days);
    // Try to mint - should still be too early
    vm.expectRevert();
    UNI.mint(alice, 1);

    // Complete warp to Year 3
    vm.warp(block.timestamp + 183 days);
    uint256 supplyBeforeYear3 = UNI.totalSupply();
    uint256 expectedYear3Mint = supplyBeforeYear3 * MINT_CAP_PERCENT / 100;

    uniMinter.mint();
    uint256 year3Balance = UNI.balanceOf(alice) - year1Balance - year2Balance;
    assertEq(year3Balance, expectedYear3Mint);
    assertEq(UNI.totalSupply(), supplyBeforeYear3 + expectedYear3Mint);

    // Warp to Year 4 with extra time to show timing flexibility
    vm.warp(block.timestamp + 400 days);
    uint256 supplyBeforeYear4 = UNI.totalSupply();
    uint256 expectedYear4Mint = supplyBeforeYear4 * MINT_CAP_PERCENT / 100;

    uniMinter.mint();
    uint256 year4Balance = UNI.balanceOf(alice) - year1Balance - year2Balance - year3Balance;
    assertEq(year4Balance, expectedYear4Mint);
    assertEq(UNI.totalSupply(), supplyBeforeYear4 + expectedYear4Mint);

    // Warp to Year 5
    vm.warp(block.timestamp + 365 days);
    uint256 supplyBeforeYear5 = UNI.totalSupply();
    uint256 expectedYear5Mint = supplyBeforeYear5 * MINT_CAP_PERCENT / 100;

    uniMinter.mint();
    uint256 year5Balance =
      UNI.balanceOf(alice) - year1Balance - year2Balance - year3Balance - year4Balance;
    assertEq(year5Balance, expectedYear5Mint);
    assertEq(UNI.totalSupply(), supplyBeforeYear5 + expectedYear5Mint);

    // Verify compound growth effect - each year's mint is 2% of an increasingly larger supply
    assertGt(year2Balance, year1Balance); // Year 2 mint > Year 1 due to compound growth
    assertGt(year3Balance, year2Balance); // Year 3 mint > Year 2
    assertGt(year4Balance, year3Balance); // Year 4 mint > Year 3
    assertGt(year5Balance, year4Balance); // Year 5 mint > Year 4
  }

  function test_revokeSplits_PreventDoubleAdjustment() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, 180);

    // Setup partial revocation
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + 365 days / 2; // Halfway through next period
    uint256 initiateTime = revocationCompleteTime - 180 days;
    vm.warp(initiateTime);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // First call reduces split and sets adjustedForRevocation = true
    uniMinter.revokeSplit(0);
    assertEq(uniMinter.totalUnits(), 2500); // 50% of 5000
    (address recipient, uint16 units,,, bool adjusted) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 2500);
    assertEq(adjusted, true);

    // Second call should revert because it's already been revoked
    vm.expectRevert(IUNIMinter.InvalidRevocation.selector);
    uniMinter.revokeSplit(0);

    // Verify split unchanged after failed second call
    assertEq(uniMinter.totalUnits(), 2500);
    (recipient, units,,, adjusted) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 2500);
    assertEq(adjusted, true);
  }

  /// @dev initiateRevokeSplit CANNOT to called twice if its already been adjusted
  function test_revokeSplit_PreventDoubleInitiateRevokeSplit() public {
    vm.prank(owner);
    uniMinter.grantSplit(alice, 5000, 180);

    // Setup partial revocation
    uint256 nextMintTime = UNI.mintingAllowedAfter();
    uint256 revocationCompleteTime = nextMintTime + 365 days / 2; // Halfway through next period
    uint256 initiateTime = revocationCompleteTime - 180 days;
    vm.warp(initiateTime);

    vm.prank(owner);
    uniMinter.initiateRevokeSplit(0);

    // First call reduces split and sets adjustedForRevocation = true
    uniMinter.revokeSplit(0);
    assertEq(uniMinter.totalUnits(), 2500); // 50% of 5000
    (address recipient, uint16 units,,, bool adjusted) = uniMinter.splits(0);
    assertEq(recipient, alice);
    assertEq(units, 2500);
    assertEq(adjusted, true);

    // Cannot re-initiate revocation on something already adjusted
    vm.prank(owner);
    vm.expectRevert(IUNIMinter.InvalidRevocation.selector);
    uniMinter.initiateRevokeSplit(0);
  }
}
